// Basic exercises involving Lists
// Advanced exercises involving Lists

type List <A: Type> {
  nil
  cons(head: A, tail: List<A>)
}

// Returns the head of the List
head<A: Type>(xs: List<A>): Maybe<A>
  case xs {
    nil: Maybe.none!
    cons: Maybe.some!(xs.head)
  }

// Returns the list without the first element
tail<A: Type>(xs: List<A>): List<A>
  case xs {
    nil: List.nil!
    cons: xs.tail
  }

// Returns the length of a list
length<A: Type>(xs: List<A>): Nat
  case xs {
    nil: Nat.zero
    cons: Nat.succ(length!(xs.tail))
  }

// Returns the last element of a list
last<A: Type>(xs: List<A>): Maybe<A>
  case xs {
    nil: Maybe.none!
    cons: case xs.tail {
            nil: Maybe.some!(xs.head)
            cons: last!(xs.tail)
           }
  }

// Returns the list without the last element
init<A: Type>(xs: List<A>): List<A>
  case xs {
    nil: xs
    cons: let new_list = reverse!(xs)
          let lista = tail!(new_list)
          reverse!(lista)
  }

// Concatenates two lists
concat<A: Type>(xs: List<A>, ys: List<A>): List<A>
  case xs {
   nil: ys
   cons: case ys {
           nil: xs
           cons: List.cons!(xs.head, concat!(xs.tail, ys))
         }
  }

// Removes n elements from the start of the list
// List.drop<Nat>(3, [1,2,3,4,5]) -> [4, 5]
drop<A: Type>(n: Nat, xs: List<A>): List<A>
  case n {
    zero: xs
    succ: case xs {
            nil: List.nil!
            cons: drop!(n.pred, xs.tail)
            }
  }

// Creates a new list with the N first elements of the previous list
// List.take<Nat>(3, [1,2,3,4,5]) -> [1,2,3]
take<A: Type>(n: Nat, xs: List<A>): List<A>
  case n {
    zero: List.nil!
    succ: case xs {
            nil: List.nil!
            cons: List.cons!(xs.head, take!(n.pred, xs.tail))
            }
  }

// Applies a function to all elements of a list
map<A: Type, B: Type>(fn: A -> B, xs: List<A>): List<B>
  case xs {
    nil: List.nil!
    cons: List.cons!(fn(xs.head), map!!(fn, xs.tail))
  }

// Returns the same list, with the order reversed
reverse<A: Type>(xs: List<A>): List<A>
  reverse.go<A>(List.nil<A>, xs)
//
reverse.go<A: Type>(new_list: List<A>, old_list: List<A>): List<A>
  case old_list {
    nil: new_list
    cons: reverse.go!(List.cons!(old_list.head, new_list), old_list.tail)
  }

type Maybe <A: Type> {
  none
  some(value: A)
}

// Returns pairs of the elements of the 2 input lists on the same index
// Ex: zip!!([1,2], ["a","b","c"]) == [{1, "a"}, {2 , "b"}]
// Ex: zip!!(["a", "b"], [3, 5])   == [{"a", 3}, {2 , "b"}]
zip<A: Type, B: Type>(xs: List<A>, ys: List<B>): List<Pair<A,B>>
  case xs {
    nil: List.nil!
    cons: case ys {
            nil: List.nil!
            cons: List.cons!(Pair.new!!(xs.head, ys.head), zip!!(xs.tail, ys.tail))
           }
  }

// Returns the smallest element of a List

// -------- Roubalheira descarada --------
// o que eu fiz? Roubei, na cara dura
// peguei a função gtn do ex_04 e
// apliquei no smallest

gtn(a: Nat, b: Nat): Bool // if A > B == True else false
  case a {
    zero: Bool.false
    succ: case b {
            zero: Bool.true
            succ: gtn(a.pred, b.pred)
            }
  }

// caso xs seja vazio, retorna zero
// caso tenha algo
// se não tiver um corpo/tail, retorna a cabeça
// se tirver uma tail, eu verifico se a
// head é maior que a head da tail
// se for, a função roda de novo
// exluindo a head
// se não for, eu crio uma nova lista só
// com a head e a tail da tail,
// excluindo a head da tail
// e rodo a função nessa nova lista

smallest(xs: List<Nat>): Nat
  case xs {
    nil: Nat.zero
    cons: case xs.tail {
            nil: xs.head
            cons: case gtn(xs.head, xs.tail.head) {
                    true: smallest(xs.tail)
                    false: smallest(List.cons!(xs.head, xs.tail.tail))
                    }
           }
  }

// Roubalheira 2, o inimigo agora é outro
// resgatei a função eql do ex_04 e usei
// junto com o smallest para a função
// delete_smallest

// Equal
eql(a: Nat, b: Nat): Bool // Given 2 numbers, it returns true if the 2 are the same and false if they are different.
  case a {
    zero: case b {
      zero: Bool.true
      succ: Bool.false
      }
    succ: case b {
      zero: Bool.false
      succ: eql(a.pred, b.pred)
      }
  }


// agora onde endoidei
// vejo se a lista é vazia, se for, retorna vazio
// caso não seja, vejo se há mais de um elemento
// caso não tenha, retorna vazio
// caso tenha mais de um elemento, vejo
// se a head é igual ao menor elemento da lista
// caso seja, retorno só a tail
// caso não seja, construo uma nova
// lista com a head mais o resultado da
// função com a head da tail

// Returns the same list without the smallest element
delete_smallest(xs: List<Nat>): List<Nat>
  case xs {
    nil: List.nil!
    cons: case eql(xs.head, smallest(xs)){
                    true: xs.tail
                    false: List.cons!(xs.head, delete_smallest(xs.tail))
                   }
            }
  

// Returns the same list, in ascending order

sort_ascending(xs: List<Nat>): List<Nat>
  case xs {
    nil: List.nil!
    cons: List.cons!(smallest(xs), (sort_ascending(delete_smallest(xs))))
  }
  

// Returns an element at an index from the list
// if there is an element at that index
//   returns Maybe.some!(element)
// else
//   returns Maybe.none!
get<A: Type>(idx: Nat, xs: List<A>): Maybe<A>
   case xs {
     nil: Maybe.none!
     cons:case idx {
            zero: Maybe.none!
            succ: case idx.pred {
                    zero: Maybe.some!(xs.head)
                    succ: get!(idx.pred, xs.tail)
                  }
          }
    }

// Removes all elements that does not match a condition
// List.filter<Nat>(Nat.odd, [1, 2, 3, 4, 5]) = [1, 3, 5]
filter<A: Type>(f: A -> Bool, xs: List<A>): List<A>
  case xs {
    nil: List.nil!
    cons: case f(xs.head) {
            false: filter!(f)(xs.tail)
            true : List.cons!(xs.head, filter!(f)(xs.tail))
           }
  }


ex_08: IO<Unit>
  IO {
    Tests._08.head(head)
    Tests._08.tail(tail)
    Tests._08.length(length)
    Tests._08.last(last)
    Tests._08.init(init)
    Tests._08.concat(concat)
    Tests._08.drop(drop)
    Tests._08.take(take)
    Tests._08.map(map)
    Tests._08.reverse(reverse)
  }

